plugins {
        id 'fabric-loom' version "${loom_version}"
        id 'maven-publish'
        id 'com.google.protobuf' version '0.9.4'
}

version = project.mod_version
group = project.maven_group

base {
        archivesName = project.archives_base_name
}

repositories {
        mavenCentral()
        maven {
                name = "FabricMC"
                url = "https://maven.fabricmc.net/"
        }
}

loom {
        splitEnvironmentSourceSets()

        mods {
                "message-mod" {
                        sourceSet sourceSets.main
                        sourceSet sourceSets.client
                }
        }
}

dependencies {
        minecraft "com.mojang:minecraft:${project.minecraft_version}"
        mappings loom.officialMojangMappings()
        modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

        modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

        implementation 'com.google.protobuf:protobuf-java:3.25.3'

        include(implementation('jakarta.persistence:jakarta.persistence-api:3.1.0'))
        include(implementation('org.hibernate.orm:hibernate-core:6.6.1.Final'))
        include(implementation('org.hibernate.orm:hibernate-hikaricp:6.6.1.Final'))
        include(implementation('org.postgresql:postgresql:42.7.3'))
        include(implementation('com.zaxxer:HikariCP:5.1.0'))
}

protobuf {
        protoc {
                artifact = 'com.google.protobuf:protoc:3.25.3'
        }
        generateProtoTasks {
                all().each { task ->
                        task.builtins {
                                java {}
                        }
                }
        }
}

sourceSets {
        main {
                java {
                        srcDir 'build/generated/source/proto/main/java'
                }
        }
}

processResources {
        inputs.property "version", project.version

        filesMatching("fabric.mod.json") {
                expand "version": inputs.properties.version
        }
}

tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
}

java {
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
}

jar {
        inputs.property "archivesName", project.base.archivesName

        from("LICENSE") {
                rename { "${it}_${inputs.properties.archivesName}"}
        }
}

publishing {
        publications {
                create("mavenJava", MavenPublication) {
                        artifactId = project.archives_base_name
                        from components.java
                }
        }

        repositories {
        }
}
